FOL {
  Formula = BiImplicationFormula

  BiImplicationFormula
    = BiImplicationFormula ("<->" | "↔") ImplicationFormula -- biImplication
    | ImplicationFormula

  ImplicationFormula
    = ImplicationFormula ("->" | "→") OrFormula -- implication
    | OrFormula

  OrFormula
    = OrFormula ("||" | "|" | "∨") AndFormula -- or
    | AndFormula

  AndFormula
    = AndFormula ("&&" | "&" | "∧") UnaryFormula -- and
    | UnaryFormula

  UnaryFormula
    = "(" Formula ")" -- paren
    | ("forall" | "∀") BoundVariable "." Formula -- universal
    | ("exists" | "∃") BoundVariable "." Formula -- existential
    | ("!" | "¬") UnaryFormula -- not
    | Relation
    | BooleanLiteral

  RelationName (a relation)
    = ident

  FunctionName (a function)
    = ident

  BoundVariable (a bound variable)
    = ident

  Relation
    = Term "=" Term -- equality
    | RelationName "(" TermList ")" -- regular

  BooleanLiteral
    = True
    | False

  True = "tt" | "⊤"

  False = "ff" | "⊥"

  Term
    = Function
    | ConstantOrBoundVariable

  ConstantOrBoundVariable (a bound variable or a constant)
    = ident

  ident (an identifier)
    = letter+

  Function
    = FunctionName "(" TermList ")"

  TermList
    = NonemptyListOf<Term, ",">
}
